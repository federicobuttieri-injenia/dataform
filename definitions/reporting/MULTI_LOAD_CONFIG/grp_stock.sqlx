config {
    type: "table",
    schema: "GRP",
    name: "stock_full"
}

pre_operations {
	DECLARE input_partitions ARRAY<INT64>;
	
	SET input_partitions = ARRAY(SELECT DISTINCT giorno
		FROM (
			SELECT contesto, MAX(giorno) AS giorno
			FROM ${ref("DM_SELLOUT", "F_GIACENZA_*")} AS g 
			JOIN ${ref("DM_SELLOUT", "D_PORTA")} AS p ON (g.negozio = p.porta)
			WHERE 
				giorno >= DATE_SUB(CURRENT_DATE(), INTERVAL 5 DAY)
			AND contesto IN ('003', '004', '005', '006', '007', '008', '009', '010', '011', '002', '012', '013', '014', '015', '016', '017', '018', '019', '020', '021', '022', '023', '024', '025', '027', '057', '058', '059', '116', '194', '688', '234', '704', '717', '730', '743', '756', '769', '782', '821', '847', '051', '052', '053', '054', '055', '056', '349', '113', '118', '196', '695', '699', '711', '724', '737', '789', '068', '069', '070', '071', '072', '073', '109', '114', '192', '694', '700', '701', '788', '736', '723', '710', '046', '047', '048', '049', '050', '110', '115', '691', '733', '707', '824', '060', '061', '062', '063', '064', '065', '066', '067', '111', '189', '697', '765', '713', '830', '117', '180', '190', '692', '185', '195', '708', '721', '825', '799', '786', '352', '464', '689', '822', '744', '731', '718', '705', '350', '462', '354', '466', '487', '351', '372', '463', '687', '703', '355', '467', '356', '468', '698', '753', '740', '714', '727')
			GROUP BY 1
		));
}

SELECT
	societa, 
	CAST(anno AS STRING) AS anno, 
	CAST(stagione AS STRING) AS stagione, 
	codice_item_planning, 
	barcode_negozio AS barcode, 
	SUM(qta) as qta
FROM (
	SELECT
		societa, 
		anno, 
		stagione, 
		codice_item_planning, 
		barcode_negozio, 
		SUM(qta + qta_impegnata) AS qta
	FROM (
		SELECT
			g.barcode_negozio,
			g.giorno,
			g.negozio,
			g.dislocazione_fisica,
			g.qta,
			g.qta_transito,
			ANY_VALUE(g.societa) AS societa, 
			ANY_VALUE(g.anno) AS anno, 
			ANY_VALUE(g.stagione) AS stagione, 
			ANY_VALUE(codice_item_planning) AS codice_item_planning, 
			SUM(IFNULL(imp_b2b.qta_impegnata, 0)) AS qta_impegnata
		FROM ${ref("DM_SELLOUT", "F_GIACENZA_*")} AS g
		LEFT JOIN (
			SELECT
				barcode_negozio,
				giorno,
				negozio,
				dislocazione_fisica,
				SUM(imp.qta_impegnata) AS qta_impegnata
			FROM ${ref("DM_SELLOUT", "F_GIACENZA_*")}, UNNEST(impegnato) AS imp
			WHERE 
				giorno IN UNNEST(input_partitions)
			AND imp.causale = "B2B"
			GROUP BY 1,2,3,4
		) AS imp_b2b ON(
			g.barcode_negozio = imp_b2b.barcode_negozio
		AND g.giorno = imp_b2b.giorno
		AND IFNULL(g.negozio, "") = IFNULL(imp_b2b.negozio, "")
		AND IFNULL(g.dislocazione_fisica, "") = IFNULL(imp_b2b.dislocazione_fisica, "")
		)
		JOIN ${ref("DM_SELLOUT_STAGING", "D_BOX_BULK")} AS dbb ON (
			g.societa = dbb.societa AND g.dislocazione_logica = dbb.codice_industria
		)
		WHERE 
			g.giorno IN UNNEST(input_partitions)
		GROUP BY 1,2,3,4,5,6
		QUALIFY RANK() OVER(PARTITION BY g.dislocazione_fisica ORDER BY g.giorno DESC) = 1
	) AS input
	GROUP BY 1,2,3,4,5
		
	UNION DISTINCT
	
	SELECT 
		g.societa, 
		anno, 
		stagione, 
		codice_item_planning, 
		barcode_negozio, 
		qta
	FROM ${ref("DM_SELLOUT", "F_GIACENZA_*")} AS g 
	JOIN ${ref("DM_SELLOUT_STAGING", "D_BOX_BULK")} AS dbb ON (
		g.negozio = dbb.codice_bms AND dbb.tipologia = "BULK"
	)
	WHERE 
		giorno IN UNNEST(input_partitions)
	QUALIFY RANK() OVER(PARTITION BY codice_board ORDER BY giorno DESC) = 1 
)
GROUP BY 1,2,3,4,5
		